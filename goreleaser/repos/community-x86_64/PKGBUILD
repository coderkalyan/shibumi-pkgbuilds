# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname='goreleaser'
pkgver=1.0.0
pkgrel=1
pkgdesc='Deliver Go binaries as fast and easily as possible'
url='https://goreleaser.com'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/goreleaser/goreleaser/archive/v${pkgver}.tar.gz")
sha512sums=('5bab85717b91ea3245ff532bede24659c477f55c45c50e2f786ab270454a434ddf0bbd461a8509a0b8ca884364842e244634ff1d56f3e25b8e2efbb6935c7950')
b2sums=('0793ddac2bd67e93ed55a574fe086105a78f55e0d97d0ce3733546525ddda287dc96f04335abe5610d1cd9530f776c01975ad565316b5e4d5803571c9a6f0585')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external -X main.version=${pkgver}" .
  chmod +x ./goreleaser
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 ./goreleaser "${pkgdir}/usr/bin/goreleaser"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  ./goreleaser completion bash > "${pkgdir}/usr/share/bash-completion/completions/goreleaser"
  ./goreleaser completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_goreleaser"
  ./goreleaser completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/goreleaser.fish"
}
