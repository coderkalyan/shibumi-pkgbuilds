# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=ko
pkgver=0.9.1
pkgrel=1
pkgdesc="Build and deploy Go applications on Kubernetes"
arch=('x86_64')
url="https://github.com/google/ko"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/google/ko/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('29fe489e68f625ad640796694b5dc05dcd92712c9d8effced221abbed17af4a8206a4740f26ea0abf3cf37586a0dbd18c3bd84c4c88ff81720dfc4f1911a8eab')
b2sums=('ef2ada59476202a5212bcf7bdc350129d0c205d9179f2c5818764abb44ae485de35ec51964dd4832146658667809943001e8d1c0621010bdb540e5c1f99c1e60')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external" .
  chmod +x ./ko
}

check() {
  cd "${pkgname}-${pkgver}"
  # currently disabled
  #go test -v -x ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 ./ko "${pkgdir}/usr/bin/ko"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  ./ko completion > "${pkgdir}/usr/share/bash-completion/completions/ko"
  ./ko completion --zsh > "${pkgdir}/usr/share/zsh/site-functions/_ko"
}
